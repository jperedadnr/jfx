/*
 * Jenkins Pipeline script for building OpenJFX. Final SDKs are stored as job artifacts.
 *
 * Target: windows-x86_32
 * Versions: dynamic
 *
 * Mandatory Job Parameters:
 *   - REPO:  defines the repository to clone: git@github.com:${REPO}.git
 *   - TAG:   defines the tag to checkout: refs/tags/${TAG}
 *
 * Optional Job Parameters:
 *   - MILESTONE_FCS:  if set to false: build an early access release; if set to true: build a milestone release
 *   - RUN_TESTS:      if set to true: runs JavaFX unit tests after the dynamic gradle build
 *   - BUILD_DYNAMIC:  builds the dynamic SDK for use with a regular JVM
 *   - COMPILE_MEDIA:  enable compiling native media components
 *   - COMPILE_WEBKIT: enable compiling native webkit
 */

pipeline {
    agent {
        label 'aws && windows && openjfx'
    }

    environment {
        VS150COMNTOOLS = "C:\\progra~2\\micros~1\\2019\\Community\\VC\\Auxiliary\\Build"
        MSVC_VER = "14.27.29110"
        MSVC_REDIST_VER = "14.27.29016"
        WINDOWS_CRT_VER = "142"
    }

    options {
        skipDefaultCheckout()
        copyArtifactPermission('OpenJFX-build')
    }

    tools {
        jdk 'openjdk-15 32bit'
    }

    stages {
        stage('Verify required REPO parameter') {
            when {
                expression {
                    return params.REPO == ''
                }
            }
            steps {
                error("REPO parameter is required.")
            }
        }

        stage('Verify required TAG parameter') {
            when {
                expression {
                    return params.TAG == ''
                }
            }
            steps {
                error("TAG parameter is required.")
            }
        }

        stage('Clone repository') {
            steps {
                sh "git clone git@github.com:${env.REPO}.git repo"
                dir("repo") {
                    sh "git config remote.origin.url git@github.com:${env.REPO}.git"
                    sh "git fetch --tags --progress git@github.com:${env.REPO}.git +refs/heads/*:refs/remotes/origin/*"
                }
            }    
        }

        stage('Checkout tag') {
            steps {
                dir("repo") {
                    sh "git checkout -f `git rev-parse ${env.TAG}^{commit}`"
                }
            }    
        }

        stage('Clean repository') {
            steps {
                dir("repo") {
                    sh "git clean -d -x -f"
                }
            }
        }

        /*
         * Load generic environment variables.
         * MAJOR_VERSION: determined from the "jfx.release.*.version" properties in build.properties (i.e. 17, 17.0.1)
         * REPO_ACCOUNT: the GitHub account name of the repository that is being cloned
         */
        stage('Load generic env vars') {
            steps {
                dir("repo") {
                    script {
                        env.MAJOR_VERSION = """${sh(
                            returnStdout: true,
                            script: '''#!/bin/bash -xe
                                JFX_MAJOR=$(cat build.properties | grep "^jfx\\.release\\.major\\.version" | cut -d= -f2 | tr -d "\\n")
                                JFX_MINOR=$(cat build.properties | grep "^jfx\\.release\\.minor\\.version" | cut -d= -f2 | tr -d "\\n")
                                JFX_SECURITY=$(cat build.properties | grep "^jfx\\.release\\.security\\.version" | cut -d= -f2 | tr -d "\\n")
                                JFX_PATCH=$(cat build.properties | grep "^jfx\\.release\\.patch\\.version" | cut -d= -f2 | tr -d "\\n")
                                if [[ ${JFX_PATCH} -gt 0 ]]; then
                                    echo -n "${JFX_MAJOR}.${JFX_MINOR}.${JFX_SECURITY}.${JFX_PATCH}"
                                elif [[ ${JFX_MINOR} -gt 0 || ${JFX_SECURITY} -gt 0 ]]; then
                                    echo -n "${JFX_MAJOR}.${JFX_MINOR}.${JFX_SECURITY}"
                                else
                                    echo -n "${JFX_MAJOR}"
                                fi
                            '''
                        )}"""
                        env.REPO_ACCOUNT = """${sh(
                            returnStdout: true,
                            script: 'echo ${REPO} | cut -d/ -f1 | tr -d "\\n"'
                        )}"""
                    }
                }
            }
        }

        /*
         * Load environment variables specific to fork builds.
         * PROMOTED_BUILD_NUMBER: set to the TAG parameter
         */
        stage('Load env vars (fork)') {
            when {
                not {
                    environment name: 'REPO', value: 'gluonhq/gluonjfx'
                }
            }
            steps {
                dir("repo") {
                    script {
                        env.PROMOTED_BUILD_NUMBER = """${sh(
                            returnStdout: true,
                            script: 'echo ${TAG} | tr -d "\\n"'
                        )}"""
                    }
                }
            }
        }

        /*
         * Load environment variables specific to official builds.
         * PROMOTED_BUILD_NUMBER: set to the promoted build number of the promoted tag (i.e. number 3 from the tag 17+3-gluon)
         */
        stage('Load env vars (official)') {
            when {
                environment name: 'REPO', value: 'gluonhq/gluonjfx'
            }
            steps {
                dir("repo") {
                    script {
                        env.PROMOTED_BUILD_NUMBER = """${sh(
                            returnStdout: true,
                            script: 'echo ${TAG} | cut -d- -f1 | cut -d+ -f2 | tr -d "\\n"'
                        )}"""
                    }
                }
            }
        }

        stage('Gradle dynamic build') {
            when {
                expression {
                    return params.BUILD_DYNAMIC
                }
            }
            steps {
                dir("repo") {
                    sh "chmod +x gradlew.bat"
                    sh "./gradlew.bat --no-daemon -PCONF=Release -PMILESTONE_FCS=${env.MILESTONE_FCS} -PMAVEN_PUBLISH=true --info --refresh-dependencies -PPROMOTED_BUILD_NUMBER=${env.PROMOTED_BUILD_NUMBER} -PHUDSON_BUILD_NUMBER=${env.BUILD_NUMBER} -PHUDSON_JOB_NAME=${env.JOB_BASE_NAME} -PUSE_DEPEND=false -PBUILD_SRC_ZIP=true -PCOMPILE_WEBKIT=${env.COMPILE_WEBKIT} -PCOMPILE_MEDIA=${env.COMPILE_MEDIA} -PBUILD_LIBAV_STUBS=true -PSTUB_RUNTIME= all publishToMavenLocal"
                }
            }
        }

        stage('Run unit tests') {
            when {
                expression {
                    return params.BUILD_DYNAMIC && params.RUN_TESTS
                }
            }
            steps {
                dir("repo") {
                    sh "chmod +x gradlew.bat"
                    sh "./gradlew.bat --no-daemon -PCONF=Release -PMILESTONE_FCS=${env.MILESTONE_FCS} -PMAVEN_PUBLISH=true --info --refresh-dependencies -PPROMOTED_BUILD_NUMBER=${env.PROMOTED_BUILD_NUMBER} -PHUDSON_BUILD_NUMBER=${env.BUILD_NUMBER} -PHUDSON_JOB_NAME=${env.JOB_BASE_NAME} -PUSE_DEPEND=false -PBUILD_SRC_ZIP=true -PCOMPILE_WEBKIT=${env.COMPILE_WEBKIT} -PCOMPILE_MEDIA=${env.COMPILE_MEDIA} -PBUILD_LIBAV_STUBS=true -PSTUB_RUNTIME= test"
                }
            }
        }

        stage('Archive dynamic zips') {
            when {
                expression {
                    return params.BUILD_DYNAMIC
                }
            }
            steps {
                dir("repo") {
                    archiveArtifacts artifacts: 'build/artifacts/bundles/javafx-jmods-*.zip,build/artifacts/bundles/javafx-sdk-*.zip,build/publications/*-win.jar', fingerprint: true
                }
            }
        }
    }
}
